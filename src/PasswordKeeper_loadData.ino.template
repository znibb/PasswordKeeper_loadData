#include <Arduino.h>
#include <EEPROM.h>

// Variables
int memAddr = 0;

// Parameters
const byte uid_size = 7;        // Max length of uid array
const byte password_size = 20;  // Max length of password char array

// Struct defining uid/password pair
struct tagData{
  byte uid[uid_size];
  char password[password_size];
};

// ###############################
// ########## User data ##########
// ###############################
struct tagData tags[] = {
  {{0xFF, 0xFF, 0xFF, 0xFF},                    "test1\n"},
  {{0xFE, 0xFE, 0xFE, 0xFE},                    "test2\n"},
  {{0xFD, 0xFD, 0xFD, 0xFD, 0xFD, 0xFD, 0xFD},  "test3\n"},
  {{0xFC, 0xFC, 0xFC, 0xFC},                    "test4\n"}
};
// ###############################
// ###############################
// ###############################

// Count how many tags were entered
const byte nbrOfTags = sizeof(tags)/sizeof(tagData);

void setup(){
  // Initialize serial connecton
  Serial.begin(115200);

  // Wait for serial connection
  while(!Serial){
    ;
  }

  // Calculate size of the data the user wants to store
  int memUsage = sizeof(nbrOfTags) + sizeof(uid_size) + sizeof(password_size) + sizeof(tags);

  // Break if the required memory is larger than the available memory
  if(memUsage > EEPROM.length()){
    Serial.print("Size of information (");
    Serial.print(memUsage);
    Serial.print(" byte) exceeds EEPROM limit (");
    Serial.print(EEPROM.length());
    Serial.println(" byte).");
    return;
  }

  // Store number of tags in first byte
  EEPROM.put(memAddr, nbrOfTags);
  memAddr += sizeof(nbrOfTags);

  // Store uid_size in second byte
  EEPROM.put(memAddr, uid_size);
  memAddr += sizeof(uid_size);

  // Store password_size om third byte
  EEPROM.put(memAddr, password_size);
  memAddr += sizeof(password_size);

  // Write data struct to memory
  EEPROM.put(memAddr, tags);
  Serial.print("Successfully wrote ");
  Serial.print(nbrOfTags);
  Serial.print(" entries (");
  Serial.print(memUsage);
  Serial.println(" byte).");

  // Print additional information regarding memory usage
  //byte maxNbrOfTags = (EEPROM.length()-3)/(uid_size*password_size)
  Serial.print("With an ID size of ");
  Serial.print(uid_size);
  Serial.print(" bytes and a password length of ");
  Serial.print(password_size);
  Serial.print(" characters you could fit a maximum of ");
  Serial.print((EEPROM.length()-3)/sizeof(tagData));
  Serial.println(" entries into memory.");
}

void loop(){
  // Infinite loop when done
  delay(10000);
}
